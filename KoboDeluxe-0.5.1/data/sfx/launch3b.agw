/////////////////////////////////////////////
// Teleport launch, short version
// Copyright (C) 2002, 2007 David Olofson
/////////////////////////////////////////////

d = .7;

w_format target, MONO16, 16000;
w_blank target, 16000*d, 0;

//noise
w_env FREQUENCY, 0, 8000;
w_env AMPLITUDE,
		d*.01,	.1,
		d*.01,	.15,
		d*.01,	.02,
		d*.15,	.03,
		d*.15,	.01,
		d*.17,	0;
w_env MOD1, 1;
w_osc target, NOISE;

//bp filter sweep
w_env FREQUENCY,
		0,	4000,
		d*.1,	2000,
		d*.15,	2000,
		d*.01,	1000;
w_env AMPLITUDE, 0, 13;
w_env MOD1, 3;
w_filter target, BANDPASS_12;

//electro humming
w_env AMPLITUDE,
		d*.01,	.1,
		d*.14,	.3,
		d*.35,	.1,
		d*.55,	0;
w_env FREQUENCY,
		0,	59,
		d*.5,	60,
		d*.5,	180;
w_env MOD1,
		d*.2,	.7,
		d*.2,	2,
		d*.1,	0;
w_osc target, SINE;

//electro buzz
w_env AMPLITUDE,
		d*.01,	.1,
		d*.14,	.05,
		d*.3,	.1,
		d*.05,	.05,
		d*.55,	0;
w_env FREQUENCY,
		0,	59,
		d*.5,	60,
		d*.5,	180;
w_env MOD1,
		0,	.5,
		d*.2,	.6;
w_osc target, PULSE;

//tecno squeal
w_env AMPLITUDE,
		d*.01,	.03,
		d*.14,	.1,
		d*.05,	.05,
		d*.8,	0;
w_env MOD1,
		d*.3,	2,
		d*.7,	0;
procedure ts(f0)
{
	w_env FREQUENCY,
			0,	f0,
			d*.3,	f0*1.1+400,
			d*.1,	f0,
			d*.6,	f0*3;
	w_osc target, SINE;
}
ts(200);
ts(700);
ts(1500);
ts(2500);
ts(4000);
ts(5000);
