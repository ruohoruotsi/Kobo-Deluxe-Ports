/////////////////////////////////////////////
// Explo 6
// Copyright (C) 2007 David Olofson
/////////////////////////////////////////////

w_format target, MONO16, 24000;
w_blank target, 24000*4, 0;

procedure f(tp, f0, res)
{
	w_env FREQUENCY, f0;
	w_env AMPLITUDE, res;
	w_filter target, tp;
}

// Rumble
w_env AMPLITUDE, .5;
w_env FREQUENCY, 150;
w_osc target, NOISE;

f LOWPASS_12, 150, 5;

// Crackle
w_env AMPLITUDE, .5;
w_env FREQUENCY, 300;
w_osc target, NOISE;
w_env FREQUENCY, 50;
w_osc target, NOISE;

// Attack
w_env AMPLITUDE,
		0,	.5,
		.5,	.1,
		3,	0;
w_env FREQUENCY, 6000;
w_osc target, NOISE;
w_env FREQUENCY, 2000;
w_osc target, NOISE;

// Mod
w_env AMPLITUDE, 1;
w_env FREQUENCY, 25;
w_env BALANCE,
		0,	.7,
		1.5,	.2;
w_osc target, NOISE, MUL;

// Resonances
f PEAK_12, 45, 5;
f PEAK_12, 110, 12;
f PEAK_12, 220, 15;
f PEAK_12, 400, 7;
f PEAK_12, 600, 3;
f PEAK_12, 1100, 2;
f PEAK_12, 1900, 4;
f PEAK_12, 3200, 3;

// Treble damping
w_env FREQUENCY,
		0,	24000,
		.5,	6000,
		1.25,	1500;
w_filter target, LOWPASS_6;
w_env FREQUENCY, 60;
w_env AMPLITUDE, 1;
w_env MOD1,
		0,	1,
		.5,	.5,
		1,	.3,
		2.5,	.1;
w_filter target, LOWPASS_12;

w_env AMPLITUDE,
		0, 	1,
		2,	.8,
		1,	.2,
		1,	0;
w_gain target;
