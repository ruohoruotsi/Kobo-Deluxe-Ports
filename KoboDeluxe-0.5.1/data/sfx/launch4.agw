/////////////////////////////////////////////
// Boss teleport launch
// Copyright (C) 2002, 2007 David Olofson
/////////////////////////////////////////////

w_format target, MONO16, 16000;
w_blank target, 16000, 0;

//noise
w_env FREQUENCY, 0, 8000;
w_env AMPLITUDE,
		.01,	.1,
		.01,	.15,
		.01,	.02,
		.15,	.03,
		.15,	.01,
		.17,	0;
w_env MOD1, 1;
w_osc target, NOISE;

//bp filter sweep
w_env FREQUENCY,
		0,	4000,
		.1,	2000,
		.15,	2000,
		.01,	1000;
w_env AMPLITUDE, 0, 13;
w_env MOD1, 3;
w_filter target, BANDPASS_12;

//electro buzz
w_env AMPLITUDE,
		.01,	.2,
		.14,	.05,
		.3,	.1,
		.2,	0;
w_env FREQUENCY,
		0,	30,
		.3,	60,
		.2,	30,
		.2,	10;
w_env MOD1,
		0,	.5,
		.5,	.9;
w_osc target, PULSE;

//tecno squeal
w_env AMPLITUDE,
		.01,	.03,
		.14,	.1,
		.05,	.05,
		.8,	0;
w_env MOD1,
		.3,	2,
		.5,	0;
procedure ts(f0)
{
	w_env FREQUENCY,
			0,	f0,
			.3,	f0*1.1+400,
			.4,	f0/2,
			.3,	f0*3;
	w_osc target, SINE;
}
ts(150);
ts(500);
ts(1500);
ts(2500);
ts(4500);
ts(6500);
